plugins {
    id 'org.jetbrains.intellij'
    id 'org.jetbrains.kotlin.jvm' version '1.4.30'
}

group 'io.nimbly.tzatziki'

dependencies {
    implementation project(':common')

    implementation("com.openhtmltopdf:openhtmltopdf-core:1.0.8")
    implementation("com.openhtmltopdf:openhtmltopdf-pdfbox:1.0.8")
    implementation("com.openhtmltopdf:openhtmltopdf-java2d:1.0.8")
    implementation("com.openhtmltopdf:openhtmltopdf-svg-support:1.0.8")

    implementation("org.freemarker:freemarker:2.3.23")
    implementation('com.github.rjeschke:txtmark:0.13')

    runtimeOnly project(':extensions:java')
    runtimeOnly project(':extensions:kotlin')
    runtimeOnly project(':extensions:javascript')
}

configurations {
    all*.exclude group: "xml-apis", module: "xml-apis"
    all*.exclude group: "xml-apis", module: "xml-apis-ext"
}

compileKotlin { kotlinOptions.jvmTarget = "11" }
compileTestKotlin { kotlinOptions.jvmTarget = "11" }

// See https://github.com/JetBrains/gradle-intellij-plugin/
intellij {
    version = rootProject.ext.plugin_idea_version
    plugins = rootProject.ext.plugin_idea_plugins
}

tasks.runPluginVerifier {
    ideVersions("IC-203.7148.40, CL-211.5787.12, PS-211.5787.18") //IC-203.7148.40, IU-203.7148.40, CL-211.5787.12, PS-211.5787.18
}

// https://intellij-support.jetbrains.com/hc/en-us/community/posts/360000620084-how-to-build-a-plugin-that-is-compatible-to-multiple-versions-of-IntelliJ-
patchPluginXml {
    sinceBuild '202'
    untilBuild '211.*'

    changeNotes rootProject.ext.changeNotes
}

tasks.buildSearchableOptions {
    enabled = false
}

jar {
    archiveBaseName = 'tzatziki'
}

publishPlugin {
    token = System.getenv("ORG_GRADLE_PROJECT_intellijPublishToken")
}

